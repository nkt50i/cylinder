
                // Параметры прямоугольника
                L = 1;      // Ширина прямоугольника
                H = 1;      // Высота прямоугольника

                // Параметры кругового выреза
                r1 = 0.125;  // Малый радиус эллипса
                r2 = 0.125; // Большой радиус эллипса
                l = L/2;   // X-координата центра круга
                h = H/2;   // Y-координата центра круга

                // Размеры сетки
                N = 30;     // Количество точек на границе
                d = H/N;    // Размер сетки по ширине канала
                dd = 0.025*d; // Размер сетки на окружности

                // Точки для прямоугольника
                Point(1) = {0, 0, 0, d};
                Point(2) = {L, 0, 0, d};
                Point(3) = {L, H, 0, d};
                Point(4) = {0, H, 0, d};

                // Линии для прямоугольника
                Line(1) = {1, 2};  // Нижняя граница
                Line(2) = {2, 3};  // Правая граница
                Line(3) = {3, 4};  // Верхняя граница
                Line(4) = {4, 1};  // Левая граница

                // Точки для эллипса
                Point(5) = {l, h, 0, dd};
                Point(6) = {l + r2, h, 0, dd};
                Point(7) = {l, h + r1, 0, dd};
                Point(8) = {l - r2, h, 0, dd};
                Point(9) = {l, h - r1, 0, dd};

                // Дуги для эллипса
                Ellipse(5) = {6, 5, 7};
                Ellipse(6) = {7, 5, 8};
                Ellipse(7) = {8, 5, 9};
                Ellipse(8) = {9, 5, 6};

                // Контур прямоугольника
                Line Loop(1) = {1, 2, 3, 4};

                // Контур круга
                Line Loop(2) = {5, 6, 7, 8};

                Rotate {{0, 0, 1}, {l, h, 0}, Pi / 4} {
                    Curve{5, 6, 7, 8}; // Вращаем все дуги круга
                }

                // Плоская поверхность с вырезом
                Plane Surface(1) = {1, 2};

                // Физические линии для границ
                Physical Line("1") = {1};           // Нижняя граница
                Physical Line("2") = {3};           // Верхняя граница
                Physical Line("3") = {4};           // Левая граница
                Physical Line("4") = {2};           // Правая граница
                Physical Line("5") = {5, 6, 7, 8};  // Граница круга

                // Физическая поверхность
                Physical Surface("1") = {1};

                // Настройка сетки
                Mesh.CharacteristicLengthMin = dd;
                Mesh.CharacteristicLengthMax = d;
            